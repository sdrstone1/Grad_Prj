/*
 * Copyright (c) 2019. KollHong. All Rights Reserved.
 * Copyright (c) 2018. KollHong. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.tistory.kollhong.arduino_bluetooth"
        minSdkVersion 22
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug{
            minifyEnabled false
            crunchPngs false
        }
    }

    flavorDimensions "stage"
    productFlavors {
        beta {
            dimension "stage"
            applicationIdSuffix ".beta"
            versionCode 100
            versionName "1.0.0"
            buildConfigField 'boolean', 'isTEST', "true"
            //Log.d, Toast에 if(BuildConfig.isTEST) 로 분기
            minSdkVersion 22
            resConfigs "ko", "xxxhdpi"
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
        }
        rel {
            dimension "stage"
            applicationIdSuffix ".rel"
            versionCode new Date().format('yyMMddHHmm').toInteger()
            versionName "1.0.0"
            buildConfigField 'boolean', 'isTEST', "false"
            //Log.d, Toast에 if(BuildConfig.isTEST) 로 분기
            minSdkVersion 22
        }

    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk false
        }
    }
    variantFilter{ variant ->
        def flavorName = variant.getFlavors().get(0).name
        splits.abi.enable = (flavorName == 'rel')
    }

    dexOptions {
        preDexLibraries true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:support-vector-drawable:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.github.hadiidbouk:ChartProgressBar-Android:2.0.6'
    implementation 'com.github.danielnilsson9:color-picker-view:1.4.0@aar'
}
